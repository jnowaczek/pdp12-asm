/TOGGLE-IN (OR RIM/BIN IN) BOOTLOADER
/LOAD AT 0017
/START SERVER ON PC
/CLEAR/CONTINUE AT 0020

BASER=6400
BASET=6410
/BASER=6030
/BASET=6040

SKSF=BASER+1
SKCC=BASER+2
SKRS=BASER+4
SKRB=BASER+6

STSF=BASET+1
/STCF=BASET+2
/STPC=BASET+4
STLS=BASET+6

BOOT=0000

ADDR=	17
	*20
	CLA CMA		/AC=7777
	DCA ADDR	/SET UP ADDRESS
	TAD BOOTMS	/SEND BOOT MESSAGE TO SERVER
	STLS
	STSF
	JMP .-1
RDLP,	SKCC		/CLEAR AC AND FLAG
	SKSF		/SKIP IF FLAG SET
	JMP .-1
	SKRB		/READ BUFFER
	CLL RTL		/ROTATE 6 LEFT
	RTL
	SPA		/CHANNEL 8 PUNCHED?
	JMP BOOT	/YES, CONTINUE BOOTING
	RTL
	SKSF		/SKIP IF FLAG SET
	JMP .-1
	SKRS		/OR BUFFER WITH AC
	DCA I ADDR	/STORE CONTENTS
	JMP RDLP	/KEEP LOOPING
BOOTMS,	100		/"@" COMMAND TO SERVER
$
